buildscript {
    ext.kotlin_version = '1.2.0'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}


group 'uk.co.jrtapsell'
version '1.0-SNAPSHOT'

apply plugin: "com.moowork.gulp"
apply plugin: 'kotlin2js'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-js', version: '1.2.10'
}

task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/web/static/js/kotlin"

    from "${projectDir}/src/main/resources"
    into "${projectDir}/web/static/js/kotlin"
    dependsOn classes
}
compileKotlin2Js {
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"
    // remaining configuration options
}

task copyExtras(type: Sync) {
    from "${projectDir}/extra"
    into "${projectDir}/web"
}


assemble.dependsOn assembleWeb
assemble.dependsOn gulp_build
assembleWeb.dependsOn copyExtras

task wipeWeb(type: Delete) {
    delete 'web'
}

clean.dependsOn("wipeWeb")
